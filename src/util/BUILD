load("@oceandoc//bazel:common.bzl", "GLOBAL_COPTS", "GLOBAL_LOCAL_DEFINES")
load("//bazel:build.bzl", "cc_test")
load("//bazel:cpplint.bzl", "cpplint")

package(default_visibility = ["//visibility:public"])

COPTS = GLOBAL_COPTS + select({
    "@oceandoc//bazel:not_cross_compiling_on_windows": [
        "/Iexternal/double-conversion",
        "/Iexternal/folly",
        "/Iexternal/libsodium/src/libsodium/include",
        "/I$(GENDIR)/external/folly",
    ],
    "//conditions:default": [
        "-isystem external/double-conversion",
        "-isystem external/folly",
        "-isystem external/libsodium/src/libsodium/include",
        "-isystem $(GENDIR)/external/folly",
    ],
}) + select({
    "@platforms//os:linux": [],
    "@platforms//os:osx": [],
    "@platforms//os:windows": [],
    "//conditions:default": [],
})

LOCAL_DEFINES = GLOBAL_LOCAL_DEFINES + select({
    "@oceandoc//bazel:not_cross_compiling_on_windows": [
        "GLOG_NO_ABBREVIATED_SEVERITIES",
    ],
    "//conditions:default": ["BUILD_TRACING=0"],
}) + select({
    "@platforms//os:linux": [],
    "@platforms//os:osx": [],
    "@platforms//os:windows": [],
    "//conditions:default": [],
})

cc_library(
    name = "config_manager",
    srcs = ["config_manager.cc"],
    hdrs = ["config_manager.h"],
    copts = COPTS,
    local_defines = LOCAL_DEFINES,
    deps = [
        ":util",
        "//src/proto:cc_config",
        "@folly",
    ],
)

cc_library(
    name = "double_buffer",
    hdrs = ["double_buffer.h"],
    copts = COPTS,
    local_defines = LOCAL_DEFINES,
    deps = [
        "@com_github_glog_glog//:glog",
        "@com_google_absl//absl/synchronization",
    ],
)

cc_library(
    name = "periodic_task",
    srcs = ["periodic_task.cc"],
    hdrs = ["periodic_task.h"],
    copts = COPTS,
    local_defines = LOCAL_DEFINES,
    deps = [
        ":scan_manager",
        ":util",
        "//src/proto:cc_config",
        "//src/proto:cc_data",
        "@folly",
    ],
)

cc_library(
    name = "repo_manager",
    srcs = ["repo_manager.cc"],
    hdrs = ["repo_manager.h"],
    copts = COPTS,
    local_defines = LOCAL_DEFINES,
    deps = [
        ":scan_manager",
        ":timer",
        ":util",
        "//src/proto:cc_config",
        "//src/proto:cc_data",
        "@folly",
    ],
)

cc_test(
    name = "repo_manager_test",
    srcs = ["repo_manager_test.cc"],
    copts = COPTS,
    data = ["//conf:base_config.json"],
    local_defines = LOCAL_DEFINES,
    deps = [":repo_manager"],
)

cc_library(
    name = "scan_manager",
    srcs = ["scan_manager.cc"],
    hdrs = ["scan_manager.h"],
    copts = COPTS,
    local_defines = LOCAL_DEFINES,
    deps = [
        ":double_buffer",
        ":thread_pool",
        ":util",
        "//src/common:defs",
        "//src/proto:cc_data",
        "@folly",
    ],
)

cc_test(
    name = "scan_manager_test",
    srcs = ["scan_manager_test.cc"],
    copts = COPTS,
    data = ["//conf:base_config.json"],
    local_defines = LOCAL_DEFINES,
    deps = [":scan_manager"],
)

cc_library(
    name = "thread_pool",
    srcs = ["thread_pool.cc"],
    hdrs = ["thread_pool.h"],
    copts = COPTS,
    local_defines = LOCAL_DEFINES,
    deps = [
        ":config_manager",
        "@boost//:asio",
        "@com_github_glog_glog//:glog",
    ],
)

cc_test(
    name = "thread_pool_test",
    srcs = ["thread_pool_test.cc"],
    copts = COPTS,
    local_defines = LOCAL_DEFINES,
    deps = [":thread_pool"],
)

cc_library(
    name = "util",
    srcs = ["util.cc"] + select({
        "@platforms//os:linux": ["util_linux.cc"],
        "@platforms//os:osx": [],
        "@platforms//os:windows": [],
        "//conditions:default": [],
    }),
    hdrs = ["util.h"] + select({
        "@platforms//os:linux": ["util_linux.h"],
        "@platforms//os:osx": [],
        "@platforms//os:windows": [],
        "//conditions:default": [],
    }),
    copts = COPTS,
    local_defines = LOCAL_DEFINES,
    deps = [
        "//src/common:defs",
        "//src/proto:cc_service",
        "@boost//:algorithm",
        "@boost//:beast",
        "@boost//:iostreams",
        "@boost//:uuid",
        "@com_github_glog_glog//:glog",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/synchronization",
        "@com_google_absl//absl/time",
        "@com_google_protobuf//:protobuf",
        "@crc32c",
        "@fmt",
        "@folly",
        "@openssl",
        "@rapidjson",
        "@smhasher",
        "@xz//:lzma",
    ] + select({
        "@oceandoc//bazel:jemalloc": ["@jemalloc"],
        "@oceandoc//bazel:tcmalloc": ["@tcmalloc//tcmalloc"],
        "//conditions:default": [],
    }),
)

cc_library(
    name = "timer",
    hdrs = ["timer.h"],
    deps = [":util"],
)

cc_test(
    name = "util_test",
    timeout = "short",
    srcs = ["util_test.cc"],
    data = [
        "//conf:base_config.json",
        "//test_data/util_test:txt",
        "//test_data/util_test:txt_symlink",
        "//test_data/util_test/target_dir:test",
    ],
    deps = [":util"],
)

cc_test(
    name = "util_platform_test",
    timeout = "short",
    srcs = select({
        "@platforms//os:linux": ["util_linux_test.cc"],
        "@platforms//os:osx": [],
        "@platforms//os:windows": [],
        "//conditions:default": [],
    }),
    deps = [":util"],
)

cc_test(
    name = "config_manager_test",
    srcs = [
        "config_manager_test.cc",
    ],
    copts = COPTS,
    data = ["//conf:base_config.json"],
    local_defines = LOCAL_DEFINES,
    deps = [":config_manager"],
)

cpplint()
